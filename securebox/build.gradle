
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

ext {
    libraryVersion = "1.3.1"
    libraryGroupId = "com.ptsiogas4"
    libraryArtifactId = "secure-box"
}

afterEvaluate {
    publishing {
        publications {
            lib(MavenPublication) {
                groupId libraryGroupId
                artifactId libraryArtifactId
                artifact(sourceJar)
                artifact(bundleReleaseAar)
                version libraryVersion

                pom.withXml {
                    println "start depedencies"
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.releaseRuntimePublication.allDependencies.each { dependency ->
                        if (dependency.group != null && dependency.version != null && dependency.name != "unspecified") {
                            println "PTDepedency: ${dependency.group}:${dependency.name}:${dependency.version}"
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dependency.group)
                            dependencyNode.appendNode('artifactId', dependency.name)
                            dependencyNode.appendNode('version', dependency.version)
                        }
                    }
                    println "end releaseRuntimePublication"
                    println "end depedencies"
                }
            }
        }
    }
}

android {

    testOptions {
        unitTests.includeAndroidResources = true
    }

    compileSdkVersion configCompileSdkVersion

    defaultConfig {
        minSdkVersion configMinSdkVersion
        targetSdkVersion configTargetSdkVersion
        versionCode 2203310
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = configJavaVersion
        targetCompatibility = configJavaVersion
    }

    kotlinOptions {
        jvmTarget = configJavaVersion
    }

    buildToolsVersion = configBuildToolsVersion

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.test:monitor:1.4.0'
    implementation 'androidx.test:core-ktx:1.4.0'
    implementation 'androidx.test.ext:junit-ktx:1.1.3'
    baseGroup dependencies
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'
    androidTestImplementation "androidx.test:runner:1.4.0"
    testImplementation 'org.mockito:mockito-core:3.11.0'
    androidTestImplementation 'org.mockito:mockito-android:2.6.3'
}
repositories {
    mavenCentral()
}